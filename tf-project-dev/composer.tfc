module "service_account_pbk_composer" {
  source     = "terraform-google-modules/service-accounts/google"
  version    = "~> 4.0"
  project_id = var.project_id
  names      = ["${var.sa_composer}"]
  project_roles = [
    "${var.project_id}=>roles/composer.worker",
    "${var.project_id}=>roles/bigquery.jobUser"
  ]
}

resource "google_service_account_iam_member" "composer_service_agent_service_account" {
  service_account_id = module.service_account_pbk_composer.service_account.name
  role               = "roles/composer.ServiceAgentV2Ext"
  member             = "serviceAccount:service-${var.project_number}@cloudcomposer-accounts.iam.gserviceaccount.com"
}

resource "google_bigquery_dataset_iam_member" "sa_composer_editor_ingestion_dataset" {
  dataset_id = google_bigquery_dataset.big_query_dataset_ingestion.dataset_id
  role       = "roles/bigquery.dataEditor"
  member     = "serviceAccount:${module.service_account_pbk_composer.service_account.email}"
}

# Bucket for composer 3
module "gcs_bucket_pbk_composer" {
  source     = "terraform-google-modules/cloud-storage/google//modules/simple_bucket"
  version    = "~> 8.0"
  name       = var.gcs_pbk_composer_bucket_name
  project_id = var.project_id
  location   = var.region
  iam_members = [{
    role   = "roles/storage.objectUser"
    member = "serviceAccount:${module.service_account_pbk_composer.service_account.email}"
  }]

}

resource "google_composer_environment" "pbk_composer" {
  name   = "pbk-composer"
  region = var.region

  storage_config {
    bucket = module.gcs_bucket_pbk_composer.name
  }

  config {

    software_config {
      image_version = "composer-3-airflow-2"
      env_variables = {
        GCP_PROJECT_ID  = var.project_id
        GCS_BUCKET_NAME = var.gcs_pbk_ingestion_bucket_name
        DATASET         = var.bq_pbk_ingestion_dataset
        API_URL         = var.api_url
      }

    }

    workloads_config {
      scheduler {
        cpu        = 0.5
        memory_gb  = 2
        storage_gb = 1
        count      = 1
      }
      triggerer {
        cpu       = 0.5
        memory_gb = 1
        count     = 1
      }
      web_server {
        cpu        = 0.5
        memory_gb  = 2
        storage_gb = 1
      }
      worker {
        cpu        = 0.5
        memory_gb  = 2
        storage_gb = 1
        min_count  = 1
        max_count  = 3
      }

    }
    environment_size = "ENVIRONMENT_SIZE_SMALL"

    node_config {
      service_account = module.service_account_pbk_composer.service_account.name
    }

  }
}